buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath project.ver.androidPlugin
    }
}

apply plugin: 'com.android.library'

dependencies {
    repositories {
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenCentral()
        mavenLocal()
    }

    compile 'com.android.support:support-v4:18.0.+'

    compile 'com.mcxiaoke.volley:library:1.0.10'

    compile 'com.eyeem.potato:library:0.9.2-SNAPSHOT@aar'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okio:okio:1.5.0'
    compile 'com.squareup.mimecraft:mimecraft:1.1.0'
    compile 'com.evernote:android-job:1.1.11'
    compile 'com.esotericsoftware.kryo:kryo:2.20'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3.2'
    //testCompile 'com.squareup:fest-android:1.0.8'
}

task generateSources {
    //
    doFirst {
        println "Mjolnir source code generation..."
        exec {
            commandLine 'rm', '-rf', 'src/javagen'
        }
        exec {
            commandLine "${MJOLNIR_PATH}/mjolnir-package", '--yaml', 'mjolnir.yaml', '--packagename', 'com.eyeem.mjolnir.oauth', '--output', 'src/javagen'
        }
    }
}

android {
    compileSdkVersion project.ver.compileSdk
    buildToolsVersion project.ver.buildTools

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/javagen'
            }
        }
    }

    libraryVariants.all { variant ->
        variant.javaCompile.dependsOn generateSources
    }
    buildToolsVersion '25.0.0'
}

// SONATYPE DEPLOYMENT
// in order to deploy, run: gradle uploadArchives

apply plugin: 'maven'
apply plugin: 'signing'

version = "0.1.4-SNAPSHOT"
group = "com.eyeem.mjolnir"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { gradle.taskGraph.hasTask("assembleRelease") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        snapshotRepository(url: sonatypeSnapshotRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'Mjolnir Android'
            packaging 'aar'
            description 'Mjolnir Android core classes for building general purpose SDKs'
            url 'https://github.com/eyeem/mjolnir-android'

            scm {
                url 'scm:git@github.com:eyeem/mjolnir-android.git'
                connection 'scm:git@github.com:eyeem/mjolnir-android.git'
                developerConnection 'scm:git@github.com:eyeem/mjolnir-android.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'vishna'
                    name 'Lukasz Wisniewski'
                    email 'lukasz@eyeem.com'
                }
            }
        }
    }
}
